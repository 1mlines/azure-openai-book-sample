// Azure API Management의 구독 키, 모델이나 사용자별로 이용한 토큰 수 혹은 API 실행횟수를 산출하는 쿼리
// between (datetime("<시작일시>") .. datetime("<종료일시>"))를 지정하여 실행해야 한다.
ApiManagementGatewayLogs
| where TimeGenerated between (datetime("2024-06-01T00:00:00") .. datetime("2024-06-29T23:59:59"))
  and OperationId in ('ChatCompletions_Create', 'completions_create', 'embeddings_create')
  and IsRequestSuccess == true
| extend model_name = tostring(parse_json(BackendResponseBody)['model'])
| extend prompttokens = parse_json(parse_json(BackendResponseBody)['usage'])['prompt_tokens']
| extend completiontokens = parse_json(parse_json(BackendResponseBody)['usage'])['completion_tokens']
| extend apim_subscription_id =  ApimSubscriptionId
| extend user_name = tostring(parse_json(parse_json(TraceRecords)[0]['message']))
| summarize
    prompt_tokens = sum(todecimal(prompttokens)),
    completion_tokens = sum(todecimal(completiontokens)),
    api_call_count = count()
    by apim_subscription_id, model_name, user_name